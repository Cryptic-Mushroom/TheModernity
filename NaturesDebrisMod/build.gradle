/*
 * Copyright (c) 2020 RedGalaxy & contributors
 * All rights reserved. Do not distribute.
 *
 * For a full license, see LICENSE.txt
 */

dependencies {
    embed( project( ':NaturesDebrisAPI' ) ) {
        transitive false
    }
    embed( project( ':NaturesDebrisCore' ) ) {
        transitive false
    }
}

apply from: 'universal.gradle'
apply from: 'client.gradle'
apply from: 'server.gradle'

project.ext.implName = "Nature's Debris"

build {
    dependsOn 'signJar'
    dependsOn 'signClientJar'
    dependsOn 'signServerJar'
}

sourceSets {
    main {
        resources {
            srcDirs += "src/generated/resources"
        }
    }
    old {

    }
}


task genData(type: JavaExec) {
    dependsOn ':NaturesDebrisAPI:compileJava'
    dependsOn ':NaturesDebrisCore:compileJava'
    dependsOn 'compileJava'

    // We must give the classpath as a raw path, otherwise gradle adds :classes as a dependency to this task. Since we
    // are a dependency of :classes itself this causes a circular dependency and therefore we must not have :classes as
    // dependency. Make sure not to use 'sourceSets.main.runtimeClasspath' or something relevant here
    classpath files("$rootDir/NaturesDebrisAPI/build/classes/java/main")         \
                   + files("$rootDir/NaturesDebrisCore/build/classes/java/main") \
                   + files("$rootDir/NaturesDebrisMod/build/classes/java/main")  \
                   + configurations.runtime

    main "natures.debris.data.DataGen"
    args(
        "--all",
        "--output", "$projectDir/src/generated/resources",
        "--gameDir", "."
    )
    workingDir file("$rootDir/run")
}

processResources {
    if (!System.getenv().containsKey("SKIP_DATAGEN"))
        dependsOn 'genData'
}
