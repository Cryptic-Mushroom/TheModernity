/*
 * Copyright (c) 2020 RedGalaxy & contributors
 * All rights reserved. Do not distribute.
 *
 * For a full license, see LICENSE.txt
 */

buildscript {
    repositories {
        maven { url = 'https://files.minecraftforge.net/maven' }
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '3+', changing: true
    }
}


// ==================
// ALL PROJECTS SETUP
// ==================

allprojects {
    if( !( project.name.endsWith( "IOLib" ) ) ) {
        project.ext.implName = "Nature's Debris"

        apply plugin: 'net.minecraftforge.gradle'
        apply plugin: 'idea'
        apply plugin: 'maven-publish'
        apply plugin: 'signing'

        // Initialize basic environment properties
        apply from: "$rootDir/environment.gradle"

        // ForgeGradle setup
        minecraft {
            // Snapshot mappings: March 22, 2020, v1.15.1
            mappings channel: 'snapshot', version: '20200322-1.15.1'

            // Access transformer
            accessTransformer = file( 'src/main/resources/META-INF/accesstransformer.cfg' )
        }

        repositories {
            mavenLocal()
            mavenCentral()
            maven {
                name = "Progwml6 maven"
                url = "https://dvs1.progwml6.com/files/maven/"
            }
            maven {
                name = "ModMaven"
                url = "https://modmaven.k-4u.nl"
            }
            maven {
                url "https://io.terraforged.com/repository/maven/"
            }
            flatDir {
                dirs "$rootDir/dependencies"
            }
        }

        configurations {
            embed // External deps embedded into the JAR file
            compile.extendsFrom( embed )
        }

        dependencies {
            minecraft 'net.minecraftforge:forge:1.15.2-31.2.0'

            embed "net.shadew:ptg:1.2.0"

            compileOnly fg.deobf("mezz.jei:jei-1.15.2:6.0.0.4:api")
            runtimeOnly fg.deobf("mezz.jei:jei-1.15.2:6.0.0.4")

            compileOnly "com.terraforged:TerraForgedAPI:1.15.2-0.0.1"
            runtimeOnly fg.deobf("local:TerraForged:1.15.2-0.0.13")

            compileOnly fg.deobf("local:BiomesOPlenty:1.15.2-10.0.0.366:api")
            runtimeOnly fg.deobf("local:BiomesOPlenty:1.15.2-10.0.0.366:universal")
        }

        jar {
            doFirst {
                // Include necessary dependencies
                from configurations.embed.collect { it.isDirectory() ? it : zipTree( it ) }
            }

            manifest {
                attributes(
                    [
                        "Specification-Title"     : "Nature's Debris",
                        "Specification-Vendor"    : "Shadew",
                        "Specification-Version"   : "${ version }",
                        "Implementation-Title"    : project.ext.implName,
                        "Implementation-Version"  : "${ version }",
                        "Implementation-Vendor"   : "Shadew",
                        "Implementation-Timestamp": new Date().format( "yyyy-MM-dd'T'HH:mm:ssZ" )
                    ]
                )
            }
        }

        apply from: "$rootDir/gen_sources.gradle"
    }
}



// ==================
// ROOT PROJECT SETUP
// ==================

// We should not build anything in the root project so we remove all building tasks and only build the necessary
// subprojects. We only use the root project for running.
build {
    dependsOn.clear()

    dependsOn ':NaturesDebrisAPI:build'
    dependsOn ':NaturesDebrisMod:build'
    dependsOn ':NaturesDebrisTest:build'
}


// Configure run configurations only for root project
minecraft {
    runs {
        client {
            workingDirectory project.file( 'run' )

            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'
            property 'forge.logging.console.level', 'info'
            property 'natures.debris.ide', 'true'

            mods {
                ndebris {
                    source sourceSets.main
                }
            }
        }

        server {
            workingDirectory project.file( 'run' )

            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'
            property 'forge.logging.console.level', 'info'
            property 'natures.debris.ide', 'true'

            mods {
                ndebris {
                    source sourceSets.main
                }
            }
        }

        data {
            workingDirectory project.file( 'run' )

            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'
            property 'forge.logging.console.level', 'info'
            property 'natures.debris.ide', 'true'
            property 'natures.debris.datagen', 'true'

            args '--mod', 'ndebris', '--all', '--output', file( 'NaturesDebrisMod/src/generated/resources/' )

            mods {
                ndebris {
                    source sourceSets.main
                }
            }
        }
    }
}


// Depend on all subprojects
dependencies {
    embed( project( ':NaturesDebrisAPI' ) ) {
        transitive false
    }
    embed( project( ':NaturesDebrisCore' ) ) {
        transitive false
    }
    embed( project( ':NaturesDebrisMod' ) ) {
        transitive false
    }
    embed( project( ':NaturesDebrisTest' ) ) {
        transitive false
    }
}

// Collect classes and resources from other projects so that running includes all classes
task collectClasses( type: Copy ) {
    dependsOn ':NaturesDebrisAPI:classes'
    dependsOn ':NaturesDebrisCore:classes'
    dependsOn ':NaturesDebrisMod:classes'
    dependsOn ':NaturesDebrisTest:classes'

    outputs.upToDateWhen { t -> false }

    doFirst {
        delete "$buildDir/classes/java/main"
        file( "$buildDir/classes/java/main" ).mkdirs()
    }

    configurations.collectMany {
        it.allDependencies
    }.findAll {
        it instanceof ProjectDependency
    }.each {
        ProjectDependency dep = (ProjectDependency) it
        from( "$dep.dependencyProject.buildDir/classes/java/main" )
    }

    into( "$buildDir/classes/java/main" )
}

compileJava {
    dependsOn 'collectClasses'
}


task collectResources( type: Copy ) {
    dependsOn ':NaturesDebrisAPI:classes'
    dependsOn ':NaturesDebrisCore:classes'
    dependsOn ':NaturesDebrisMod:classes'
    dependsOn ':NaturesDebrisTest:classes'

    doFirst {
        delete "$buildDir/resources/main"
        file( "$buildDir/resources/main" ).mkdirs()
    }

    outputs.upToDateWhen { t -> false }

    configurations.collectMany {
        it.allDependencies
    }.findAll {
        it instanceof ProjectDependency
    }.each {
        ProjectDependency dep = (ProjectDependency) it
        from( "$dep.dependencyProject.buildDir/resources/main" )
    }

    into( "$buildDir/resources/main" )
}

processResources {
    dependsOn 'collectResources'
}


if( project.ext.isIDE ) {
    println "Building for IDE"
}
else {
    println "Building for production"
}
