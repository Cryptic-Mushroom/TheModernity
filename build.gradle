/*
 * Copyright (c) 2020 RedGalaxy
 * All rights reserved. Do not distribute.
 *
 * Date:   01 - 12 - 2020
 * Author: rgsw
 */

// Setup for build script itself
buildscript {
    repositories {
        maven { url = 'https://files.minecraftforge.net/maven' }
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '3.+', changing: true
    }
}

apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'eclipse'
apply plugin: 'maven-publish'
apply plugin: 'signing'


project.ext.canJarBeSigned = {
    return getVariable( 'mdKeyStore' ) != null && ( !isDeploymentEnv || isDeploymentRelease )
}

// Returns the public fingerprint, may be empty ("")
project.ext.getProjectFingerprint = {
    if( canJarBeSigned() ) {
        return getVariable( 'mdSignSHA1' ).replaceAll( ':', '' ).toLowerCase()
    }
    return ''
}

// Attempts to get a project variable and if none is found it tries to read from a system environment variable
project.ext.getVariable = { key ->
    return project.hasProperty( key ) ? project.property( key ) : ENV.containsKey( key ) ? ENV[ key ] : System.getProperty( key )
}

project.ext.ENV = System.getenv()
project.ext.isDeploymentEnv = ENV[ 'DEPLOY_ENV' ] != null && 'true'.equals( ENV[ 'DEPLOY_ENV' ] )
project.ext.isDeploymentRelease = project.isDeploymentEnv && ENV[ 'DEPLOY_BUILD_TYPE' ] != null && 'release'.equals( ENV[ 'DEPLOY_BUILD_TYPE' ] )
project.ext.buildnumber = project.isDeploymentEnv ? ENV[ 'DEPLOY_BUILD_NUMBER' ] : ''

project.ext.modid = 'modernity'

project.ext.server = false

// Make sure you have these properties declared in your computer's gradle.properties file.
// It may be any token, as long as that token has the read:packages permission granted.
project.ext.gprUser = getVariable( "gprUser" )
project.ext.gprKey = getVariable( "gprKey" )


repositories {
    mavenLocal()
    maven {
        url uri('https://maven.pkg.github.com/RedGalaxySW/Noise')
        credentials {
        }
    }
    maven {
        url uri('https://maven.pkg.github.com/RedGalaxySW/IOLib')
        credentials {
            // Make sure you have these properties declared in your computer's gradle.properties file.
            // It may be any token, as long as that token has the read:packages permission granted.
            username = project.ext.gprUser
            password = project.ext.gprKey
        }
    }
}

minecraft {
    // Snapshot mappings: May 26, 2019
    mappings channel: 'snapshot', version: '20190719-1.14.3'

    // Access transformer
    accessTransformer = file( 'src/main/resources/META-INF/accesstransformer.cfg' )

    runs {
        client {
            workingDirectory project.file( 'run' )

            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'
            property 'forge.logging.console.level', 'info'

            mods {
                modernity {
                    source sourceSets.main
                }
            }
        }

        server {
            workingDirectory project.file( 'run' )

            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'
            property 'forge.logging.console.level', 'info'

            mods {
                modernity {
                    source sourceSets.main
                }
            }
        }

        data {
            workingDirectory project.file( 'run' )

            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'
            property 'forge.logging.console.level', 'info'

            args '--mod', 'modernity', '--all', '--output', file( 'src/generated/resources/' )

            mods {
                modernity {
                    source sourceSets.main
                }
            }
        }
    }
}

dependencies {
    minecraft 'net.minecraftforge:forge:1.14.4-28.1.56'

    compile 'net.rgsw:noise:1.0.3'
    compile 'net.rgsw:io:1.0'

    // provided 'dep' // May not exist at runtime
    // deobf 'dep' // Remaps a dependency
}

jar {
    manifest {
        attributes( [
            "Specification-Title"     : "The Modernity",
            "Specification-Vendor"    : "Red Galaxy",
            "Specification-Version"   : "${ version }",
            "Implementation-Title"    : project.name,
            "Implementation-Version"  : "${ version }",
            "Implementation-Vendor"   : "Red Galaxy",
            "Implementation-Timestamp": new Date().format( "yyyy-MM-dd'T'HH:mm:ssZ" )
        ]
        )
    }

    if( server ) {
        exclude( "modernity/client/**" )
    }
}

version = 'INDEV-0.3.0'
project.ext.versionName = "The Environment Update"
project.ext.isIDE = true
group = 'net.rgsw'
archivesBaseName = 'modernity'

// Use Java 1.8
sourceCompatibility = targetCompatibility = compileJava.sourceCompatibility = compileJava.targetCompatibility = '1.8'

group = 'redgalaxy'

task signJar( dependsOn: 'reobfJar' ) {
    doLast {
        if( canJarBeSigned() ) {
            ant.signjar(
                destDir: "${ jar.archivePath.parentFile }",
                jar: "${ jar.archivePath }",
                alias: getVariable( 'mdKeyStoreAlias' ),
                storetype: "jks",
                keystore: getVariable( "mdKeyStore" ),
                storepass: getVariable( 'mdKeyStorePass' ),
                keypass: getVariable( 'mdKeyStoreKeyPass' ),
                verbose: true,
                preservelastmodified: "true"
            )
            println "Jar signed: ${ jar.archivePath }"
        }
        else {
            println "No keystore property found, jar will not be signed"
        }
    }
}

task generateSources( type: Copy ) {
    doFirst {
        project.ext.isIDE = false
        delete "$buildDir/generated-src"
    }
    outputs.upToDateWhen { false }
    from( 'src/main/java' ) {
        exclude 'runs/**'
    }
    into "$buildDir/generated-src"
    filter { line ->
        line.replaceAll( 'DynamicConstants\\.IDE', "$project.isIDE" )
            .replaceAll( 'DynamicConstants\\.SIGNED', "${ canJarBeSigned() && !project.isIDE }" )
            .replaceAll( 'DynamicConstants\\.VERSION_NAME', "\"${ project.isIDE ? "The IDE version" : project.version }\"" )
            .replaceAll( 'DynamicConstants\\.VERSION', "\"${ project.isIDE ? "IDE" : project.version }\"" )
            .replaceAll( 'DynamicConstants\\.SHA1', "\"${ project.isIDE ? "" : getProjectFingerprint() }\"" )
    }
    doLast {
        println "Generated sources, IDE: $project.ext.isIDE"
    }
}

processResources {
    doFirst {
        delete "$buildDir/resources" // Delete resources to fully clean up unused items
    }
    // Import png and ogg apart from other files as we don't want lines to be
    // replaced in such files...
    from( 'src/main/resources' ) {
        exclude '**/*.png'
        exclude '**/*.ogg'
        filter { line ->
            line.replaceAll( '\\$\\{version\\}', "$project.version" )
                .replaceAll( '\\$\\{verName\\}', "$project.versionName" )
        }
    }
    from( 'src/main/resources' ) {
        include '**/*.png'
        include '**/*.ogg'
    }

    exclude '**/unused/**'
    exclude 'src/main/resources/templates/**'
}

compileJava {
    setSource( "$buildDir/generated-src" )
    dependsOn generateSources
}

build {
    dependsOn signJar
    doLast {
        project.ext.isIDE = true
    }
}


def reobfFile = file( "$buildDir/reobfJar/output.jar" )
def reobfArtifact = artifacts.add( 'default', reobfFile ) {
    type 'jar'
    builtBy 'signJar'
}
publishing {
    publications {
        mavenJava( MavenPublication ) {
            groupId = 'redgalaxy'
            artifactId = 'TheModernity'
            version = "${ project.version }"

            artifact reobfArtifact
        }
    }
    repositories {
        maven {
            name = "GitHubPackages"
            url = uri( "https://maven.pkg.github.com/RedGalaxySW/TheModernity" )
            credentials {
                username = gprUser.toLowerCase()
                password = gprKey.toLowerCase()
            }
        }
    }
}