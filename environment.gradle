/*
 * Copyright (c) 2020 RedGalaxy & contributors
 * All rights reserved. Do not distribute.
 *
 * For a full license, see LICENSE.txt
 */

// @formatter:off
version                     = getVariable("natures.debris.version")
group                       = getVariable("natures.debris.group")
archivesBaseName            = getVariable("natures.debris.modname")

sourceCompatibility                         \
    = targetCompatibility                   \
    = tasks.compileJava.sourceCompatibility \
    = tasks.compileJava.targetCompatibility \
    = '1.8'

ext {
    SYSENV          = System.getenv()
    modid           = getVariable("natures.debris.modname")
    versionName     = getVariable("natures.debris.version.name")
    isIDE           = isIDE()

    // Make sure you have these properties declared in your computer's gradle.properties file.
    // It may be any token, as long as that token has the read:packages permission granted.
    gprUser         = getVariable("gprUser")
    gprKey          = getVariable("gprKey")
}

def canJarBeSigned() {
    return getVariable('ndKeyStore') != null && !project.ext.isIDE
}

// Returns the public fingerprint, may be empty ("")
def getProjectFingerprint() {
    if (canJarBeSigned()) {
        return getVariable('ndSignSHA1').replaceAll(':', '').toLowerCase()
    }
    return ''
}

// Attempts to get a project variable and if none is found it tries to read from a system environment variable
String getVariable(String key) {
    return rootProject.hasProperty(key)
           ? rootProject.property(key)
           : project.ext.SYSENV.containsKey(key)
             ? project.ext.SYSENV[key]
             : System.getProperty(key)
}

boolean isIDE() {
    return project.ext.SYSENV.containsKey("IDE")
           ? Boolean.parseBoolean(project.ext.SYSENV["IDE"] as String)
           : true
}

// Signs a jar file
def sign(File jar) {
    if (canJarBeSigned()) {
        ant.signjar(
            destDir:   "${jar.parentFile}",
            jar:       "${jar}",
            alias:     getVariable('ndKeyStoreAlias'),
            storetype: "jks",
            keystore:  getVariable("ndKeyStore"),
            storepass: getVariable('ndKeyStorePass'),
            keypass:   getVariable('ndKeyStoreKeyPass'),
            verbose:   true,
            preservelastmodified: "true"
        )
        println "Jar signed: ${jar}"
    } else {
        println "No keystore property found, jar will not be signed"
    }
}

project.ext.getVariable = { String key -> return getVariable(key) }
project.ext.canJarBeSigned = { return canJarBeSigned() }
project.ext.getProjectFingerprint = { return getProjectFingerprint() }
project.ext.sign = { File jar -> return sign(jar) }
